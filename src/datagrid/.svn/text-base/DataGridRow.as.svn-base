package supergrid 
{
	import com.bumpslide.ui.GridItem;
	import com.bumpslide.ui.Label;

	/**
	 * DataGridRow
	 *
	 * @author David Knape
	 * @version SVN: $Id: $
	 */
	public class DataGridRow extends GridItem 
	{
		public var grid:DataGrid;
		public var labels:Array;
		
		override protected function draw():void 
		{
			var lbl:Label;
			if(labels) while(lbl = labels.shift()) {
				destroyChild( lbl );
			}
			
			if(grid && gridItemData) {
				
				var col_num:Number = 0;
				var col_x:Number = 0;
				var col_width:Number;
			
				labels = [];
				
				for each (var col:String in grid.columns ) {				
					if(grid.columnSizes && grid.columnSizes[col_num]) {
						col_width = grid.columnSizes[col_num];
						// values of less than 1 are interpretted as percentages
						if(col_width<1) col_width = col_width*width; 
					} else {
						col_width = width/grid.columns.length;
					}
					var s:String = String(gridItemData[ col ]);
					labels.push( add( Label, { text:s, width: col_width, x: col_x, maxLines:1 } ) );
					col_num++;
					col_x+=col_width;
				}
			}
		}

		
	}
}
